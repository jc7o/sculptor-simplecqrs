Application SimpleCqrs {
    basePackage=org.sample.simplecqrs

	Module command {
		
        Entity InventoryItem {
        	gap
        	String itemId key
        	boolean activated
        	
        	Repository InventoryItemRepository {
        		gap
        		inject @InventoryItemEventRepository
        		inject @InventoryItemSnapshotRepository
        		findByKey;
        		save;
        	}
        }
        
        Service InventoryItemSnapshotter {
        	subscribe to inventoryItemTopic
        	inject @InventoryItemRepository
        	inject @InventoryItemSnapshotRepository
        }
        
        ValueObject InventoryItemSnapshot {
        	String itemId index
        	boolean activated
        	Long version
        	
        	Repository InventoryItemSnapshotRepository {
        		@InventoryItemSnapshot getLatestSnapshot(String itemId);
        		protected findByCondition(PagingParameter pagingParameter);
        		save;
        	}
        }
        
        Service InventoryFacade {
        	createInventoryItem(String itemId, String name);
        	deactivateInventoryItem(String itemId);
        	renameInventoryItem(String itemId, String newName);
        	checkInItemsToInventory(String itemId, int count);
        	removeItemsFromInventory(String itemId, int count);
        }
        
        Service InventoryItemEventPublisher {
    		publishEvent(@InventoryItemEvent event) publish to inventoryItemTopic; 
    	}
        
    	Service InventoryCommandHandler {
    		subscribe to handleInventoryItemCommand eventBus=commandBus
        	inject @InventoryItemRepository
    	}
        
        abstract CommandEvent InventoryItemCommand {
        	scaffold
        	String itemId
        }
        
        CommandEvent DeactivateInventoryItem extends @InventoryItemCommand {
        }
        
        CommandEvent CreateInventoryItem extends @InventoryItemCommand {
        	String name
        }
        
        CommandEvent RenameInventoryItem extends @InventoryItemCommand {
        	String newName
        }
        
        CommandEvent CheckInItemsToInventory extends @InventoryItemCommand {
        	int countChange
        }
        
        CommandEvent RemoveItemsFromInventory extends @InventoryItemCommand {
        	int countChange
        }
        
        abstract CommandEvent InventoryItemEvent {
        	String itemId index
        	
        	Repository InventoryItemEventRepository { 
        		List<@InventoryItemEvent> findAllAfter(String itemId, long version);
        		save(List<@InventoryItemEvent> entities);
        		protected findByCondition;
        	}
        }
        
        CommandEvent InventoryItemDeactivated extends @InventoryItemEvent {
        }
        
        CommandEvent InventoryItemCreated extends @InventoryItemEvent {
        	String name
        }
        
        CommandEvent InventoryItemRenamed extends @InventoryItemEvent {
        	String newName
        }
        
        CommandEvent ItemsCheckedInToInventory extends @InventoryItemEvent {
        	int countChange
        }
        
        CommandEvent ItemsRemovedFromInventory extends @InventoryItemEvent {
        	int countChange
        }
	
	}
	
	Module query {
		Entity InventoryItemDetails {
			String itemId key
			String name required
			int currentCount
			
			Repository InventoryItemDetailsRepository {
				findByKey;
				save;
				delete;
			}
		}
		
		Entity InventoryItemList {
			String itemId key
			String name required
			
			Repository InventoryItemListRepository {
				findByKey;
				findAll;
				save;
				delete;
			}
		}
		
		Service InventoryListView {
			subscribe to inventoryItemTopic
			inject @InventoryItemListRepository
		}
		
		Service InventoryItemDetailView {
			subscribe to inventoryItemTopic
			inject @InventoryItemDetailsRepository
		}
		
		Service ReadModelFacade {
			getInventoryItems => InventoryItemListRepository.findAll;
			getInventoryItemDetails => InventoryItemDetailsRepository.findByKey;
		}
	}

}